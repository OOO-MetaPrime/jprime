apply plugin: 'idea'

buildscript {
  ext {
    springBootVersion = '2.4.4'
  }
  repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/release" }
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
  }
}

ext {
  springCloudVersion = '2020.0.2'
}

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'

  tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/release" }
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
    maven { url "http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts" }
    maven { url "https://repo.cuba-platform.com/content/groups/work/" }
  }

  dependencyManagement {
    imports {
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
  }

  configurations {
    all*.exclude module: 'spring-boot-starter-logging'
    all*.exclude module: 'slf4j-log4j12'
  }

  test {
    failFast(true)
    useJUnitPlatform {
      excludeTags 'manualTests'
      if (System.properties['test.profile'] == 'unit') {
        excludeTags 'integrationTests'
      }
    }
  }
}

wrapper {
  gradleVersion = "6.8.3"
}
