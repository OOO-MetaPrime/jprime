apply plugin: 'idea'

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'

  tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  repositories {
    maven {
      url "https://nexus.metaprime.ru/repository/maven-public/"
      allowInsecureProtocol = true
    }
    /*
    mavenCentral()
    maven { url "https://repo.cuba-platform.com/content/groups/work/" }
    maven { url "https://repo.osgeo.org/repository/release/" }
    maven { url "https://repo.spring.io/release" }
     */
  }

  dependencyManagement {
    imports {
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${spring.versions.springCloudVersion.get()}"
    }
  }

  dependencies {
    api enforcedPlatform (storages.r2dbc.pool)
    api enforcedPlatform (storages.r2dbc.proxy)
    api enforcedPlatform (storages.r2dbc.spi)
    api enforcedPlatform (storages.r2dbc.spi.parent)
    api enforcedPlatform (storages.r2dbc.postgresql)
  }

  configurations {
    all*.exclude module: 'spring-boot-starter-logging'
    all*.exclude module: 'slf4j-log4j12'
  }

  test {
    failFast(true)
    useJUnitPlatform {
      excludeTags 'manualTests'
      if (System.properties['testProfile'] == 'unit') {
        excludeTags 'integrationTests'
      }
    }
  }
}

wrapper {
  gradleVersion = "7.4.2"
}
