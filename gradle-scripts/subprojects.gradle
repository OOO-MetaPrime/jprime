apply plugin: 'idea'

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'

  tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    options.compilerArgs += ["--add-exports", "java.base/sun.security.pkcs=ALL-UNNAMED"]
    options.compilerArgs += ["--add-exports", "java.base/sun.security.util=ALL-UNNAMED"]
    options.compilerArgs += ["--add-exports", "java.base/sun.security.x509=ALL-UNNAMED"]
  }

  tasks.withType(Jar) { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }

  repositories {
    maven {
      url "https://nexus.metaprime.ru/repository/maven-public/"
      allowInsecureProtocol = true
    }
    /*
    mavenCentral()
    maven { url "https://nexus.jmix.io/repository/releases/" }
    maven { url "https://repo.osgeo.org/repository/release/" }
    maven { url "https://repo.spring.io/release" }
     */
  }

  dependencyManagement {
    imports {
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${spring.versions.springCloudVersion.get()}"
    }
  }

  configurations {
    all*.exclude module: 'commons-logging'
    all*.exclude module: 'logback-classic'
    all*.exclude module: 'logback-core'
    all*.exclude module: 'slf4j-log4j12'
    all*.exclude module: 'spring-boot-starter-logging'
  }

  test {
    failFast(true)
    useJUnitPlatform {
      excludeTags 'manualTests'
      if (System.properties['testProfile'] == 'unit') {
        excludeTags 'integrationTests'
      }
    }
  }
}

wrapper {
  gradleVersion = "8.7"
}
